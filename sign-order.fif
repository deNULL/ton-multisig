#!/usr/bin/env fift -s
"multisig-utils.fif" include

{ ."<query-boc> <keyname-1> <keyname-2>... [-O <output-boc>] [-S <seqno>]" cr
  ."Appends new signatures to a (not yet sent) query to a multisig wallet." cr
  ."It's recommended to check that query is correct first via multisig-show-order.fif" cr
  ."If output file is omitted, adds signatures to the original file." cr
  ."Note that <keyname-N> is not the index of a private key, but its filename (without .pk extension)."
} : usage()

// == Parsing command-line arguments

variable Keynames
{
  "-O" "--output-file" 1 { =: SaveFile } option-long()
  "-S" "--seqno" 1 { parse-int =: SeqNo } option-long()

  1 { =: QueryName } nth-argument()
  {
    Keynames @ cons Keynames !
    drop
  } other-argument()
} scan-args()
2 < { show-usage() } if // Checks that there's enough arguments

// == End of parsing command-line arguments

QueryName =:? SaveFile

."Loading message from " QueryName +".boc" type cr cr

QueryName +".boc" file>B B>boc parse-external-message()
=: MessageBody =: StateInit 2=: WalletAddr

."dst_addr:     " WalletAddr .addr cr cr

StateInit null? not abort"Expected StateInit to be null"

MessageBody parse-order-message()

// sig seqno valid_until last_pubkey hash_only? request signatures

=: Signatures =: Request =: HashOnly
drop // last_pubkey will be replaced
=: ValidUntil =: SeqNo
drop // signature will be replaced

null
Signatures 256 { 
  // signs signature privkey
  64 B@+ s>
  swap 256 u>B
  rot cons cons
  -1
} dictforeach
drop


// Append keypairs specified in command line
Keynames @ reverse-list() { dup null? not } { // keypairs keynames
  uncons swap +".pk" load-keypair
  // keypairs i pubkey privkey
  def? PrimaryPublicKey {
    2dup
    =: PrimaryPrivateKey
    =: PrimaryPublicKey
  } ifnot
  swap
  3 roll
  cons cons
  swap
} while
drop
=: Keypairs

."Signing order request: " Request <s csr. cr

WalletAddr null
  SeqNo ValidUntil PrimaryPublicKey HashOnly Request Keypairs order-message() PrimaryPrivateKey sign-message()
external-message() =: ExtMessage

."Updated external message: " ExtMessage <s csr. cr
ExtMessage 2 boc+>B dup Bx. cr
SaveFile +".boc" tuck B>file
."(Saved to file " type .")" cr
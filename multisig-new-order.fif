#!/usr/bin/env fift -s
"multisig-utils.fif" include

{ ."<wallet> <dest-addr> <seqno> <amount> <keyname-1> <keyname-2>... [-B <body-boc>] [-O <output-boc>] [-E <seconds>]" cr
  ."Creates a request to a multisig wallet created by multisig-init.fif, with address loaded from file <wallet>.addr "
  ."and private keys from <keyname-i>.pk, and saves it into <output-boc>.boc ('<wallet>-query.boc' by default)." cr
  ."You can specify how many seconds this order will be alive after the -E flag. By default, it will live forever."
} : usage()

5 1000 check-arg-count()

true =: BounceFlag
3 =: SendMode

$1 =: BaseFile
$2 BounceFlag parse-load-address =: BounceFlag 2=: DestAddr
$3 parse-int =: SeqNo
$4 $>GR =: Amount

BaseFile +".addr" load-address
2dup 2constant WalletAddr
."Source wallet address = " 2dup .addr cr 6 .Addr cr

$#
dup 1- $() ="-E" {
  dup $() parse-int now + =: ReqValidUntil
  2 -
} if
dup 1- $() ="-O" {
  dup $() =: SaveFile
  2 -
} if
dup 1- $() ="-B" {
  dup $() =: BodyFile
  2 -
} if

// Load keypairs specified in command line
null swap
{
  dup 5 >=
} { // keypairs i
  dup swap $() +".pk" load-keypair
  // keypairs i pubkey privkey
  def? PrimaryPublicKey {
    2dup
    =: PrimaryPrivateKey
    =: PrimaryPublicKey
  } ifnot
  swap
  3 roll
  cons cons
  swap 1-
} while
drop
=: Keypairs

PrimaryPrivateKey drop

def? BodyFile {
  @' BodyFile +".boc" file>B B>boc
} {
  <b 0 32 u, "TEST" $, b>
} cond =: TransferBody

def? SaveFile {
  BaseFile +"-query" =: SaveFile
} ifnot


."Creating new order to transfer " Amount .GR ."to account " DestAddr 2dup BounceFlag 7 + .Addr ." = " .addr 
."seqno=0x" SeqNo x. ."bounce=" BounceFlag . cr

def? ReqValidUntil {
  ."Expiry time is " @' ReqValidUntil . ", this order will be void after that date." cr
} {
  -1 =: ReqValidUntil
  ."Expiry time is NOT set, if will be kept pending forever." cr
} cond

."Body of transfer message is " TransferBody <s csr. cr
  
// Create actual message to be sent when the order will be completed
<b
  b{01} s, BounceFlag 1 i, b{000100} s, DestAddr addr, Amount Gram, 0 9 64 32 + + 1+ u, 
  TransferBody <s 2dup s-fits? not rot over 1 i, -rot { drop TransferBody ref, } { s, } cond
b> =: IntMessage

// Pack the internal message into a request data structure to be signed
SeqNo ReqValidUntil SendMode IntMessage order-request() =: Request

."Signing order request: " Request <s csr. cr

WalletAddr null
  SeqNo -1 PrimaryPublicKey false Request Keypairs order-message() PrimaryPrivateKey sign-message()
external-message() =: ExtMessage

."New order external message: " ExtMessage <s csr. cr
ExtMessage 2 boc+>B dup Bx. cr
SaveFile +".boc" tuck B>file
."(Saved to file " type .")" cr
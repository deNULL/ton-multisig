#!/usr/bin/env fift -s
"multisig-utils.fif" include

{ ."<query1-boc> <query2-boc> <keyname> [-O <output-boc>] [-s <seqno>]" cr
  ."Merges signatures of two queries to a multisig wallet with the same bodies." cr
  ."Note that you need a valid key (in <keyname>.pk file) to perform this operation."
} : usage()

3 5 check-arg-count()

."Loading message from " $1 +".boc" type cr cr

$1 +".boc" file>B B>boc parse-external-message()
=: MessageBody1 =: StateInit1 2=: WalletAddr1

."dst_addr:     " WalletAddr1 .addr cr cr

StateInit1 null? not abort"Expected StateInit to be null"

MessageBody1 parse-order-message()

// sig seqno valid_until last_pubkey hash_only? request signatures

=: Signatures1 =: Request1 =: HashOnly1
drop // last_pubkey will be replaced
=: ValidUntil1 =: SeqNo1
drop // signature will be replaced

."Loading message from " $2 +".boc" type cr cr

$2 +".boc" file>B B>boc parse-external-message()
=: MessageBody2 =: StateInit2 2=: WalletAddr2

WalletAddr1 WalletAddr2
rot <> -rot <> or abort"Expected wallet addresses to be equal"

StateInit2 null? not abort"Expected StateInit to be null"

MessageBody2 parse-order-message()

// sig seqno valid_until last_pubkey hash_only? request signatures

=: Signatures2 =: Request2 =: HashOnly2
drop // last_pubkey will be replaced
=: ValidUntil2 =: SeqNo2
drop // signature will be replaced

$#
dup 1- $() ="-s" {
  dup $() =: SeqNo
  2 -
} if
dup 1- $() ="-O" {
  dup $() =: SaveFile
  2 -
} if

def? SeqNo {
  SeqNo1 SeqNo2 max =: SeqNo
} ifnot

def? SaveFile {
  $1 =: SaveFile
} ifnot

ValidUntil1 ValidUntil2 max =: ValidUntil

HashOnly1 HashOnly2 or =: HashOnly

HashOnly1 { Request1 } { Request1 hashu } cond =: RequestHash1
HashOnly2 { Request2 } { Request2 hashu } cond =: RequestHash2

RequestHash1 RequestHash2 <> abort"Request hashes are different"

HashOnly { RequestHash1 } { Request1 } cond =: Request

$3 +".pk" load-keypair
=: PrimaryPrivateKey
=: PrimaryPublicKey

null
Signatures1 256 { 
  // signs signature privkey
  64 B@+ s>
  swap 256 u>B
  rot cons cons
  -1
} dictforeach
drop
Signatures2 256 { 
  // signs signature privkey
  64 B@+ s>
  swap 256 u>B
  rot cons cons
  -1
} dictforeach
drop
=: Keypairs

WalletAddr1 null
  SeqNo ValidUntil PrimaryPublicKey HashOnly Request Keypairs order-message() PrimaryPrivateKey sign-message()
external-message() =: ExtMessage

."Merged external message: " ExtMessage <s csr. cr
ExtMessage 2 boc+>B dup Bx. cr
SaveFile +".boc" tuck B>file
."(Saved to file " type .")" cr
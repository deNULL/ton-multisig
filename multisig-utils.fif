"common-utils.fif" include

// initial-data()
// (n k -- cell)
{
  <b
    0 32 u,               // seqno
    0 16 u,               // pending#
    rot dup 3 -roll 8 u,  // n
    swap 8 u,             // k
    // Generating list of owners
    0 dictnew {
      over <b swap 8 u, // Saving index to builder
      @' FileBase +"-" 3 pick 1 + (.) $+ +".pk" load-generate-keypair // Loading/saving pubkey and privkey
        // .s -> data:builder index:int keylist:dict builder pubkey:bytes privkey:bytes
       
      def? PrimaryPrivateKey {
        drop
      } { // no need for this key anymore
        =: PrimaryPrivateKey
        dup =: PrimaryPublicKey
      } cond
      256 B>u@ // Converting pubkey to int
        // udict requires order: value(builder) key(pubkey) dict(keylist) n=256
      rot 256 .s b>udict!
      0= abort"Unable to store public key in the contract data"
        // .s -> data:builder index:int keylist:dict
      swap 1+ swap
    } 4 roll times
    swap drop
    dict,             // owners
    null dict,        // pending
  b>
} : initial-data()

// empty-message()
// (seqno valid_until public_key -- slice)
//
// Builds an empty (with no requests) message body, returning it as a Cell.
{
  <b
    3 roll 32 u, // seqno, initially 0
    rot 32 i,    // valid_until
    swap B,      // public_key
  b>
} : empty-message()

// order-request()
// (req_seqno req_valid_until req_mode req_body -- cell)
//
// Packs an order into a cell. The actual message body to send is req_body.
{
  <b
    4 roll 32 u,
    3 roll 32 i,
    rot 8 u,
    swap ref,
  b>
} : order-request()

// order-message()
// (seqno valid_until public_key hash_only? request keypairs -- slice)
//
// Builds message containing an order for transfer, signing it by specified list of keys.
// If hash_only is true, stores only the hash of the request (for pending requests).
{
  <b
    // seqno valid_until public_key existing? request keypairs builderO
    6 roll 32 u,          // seqno, initially 0 
    5 roll 32 i,          // valid_until
    4 roll B,             // public_key
    3 pick 1 i,           // existing?
    // existing? request keypairs builderO
    3 roll {
      rot hash tuck 
      // keypairs hash builderO hash
      <b swap 256 u, b>   // hash(request)
      ref,
    } {
      rot tuck ref,       // request
      // keypairs request builderO
      swap hash swap
    } cond
    2 exch
    // builderO hash keypairs
    
    dictnew // -> builderO hash keypairs dict
      // cycle through keypairs, generate and store signatures
      { 
        swap dup null? not
      } {
        uncons uncons -rot // -> builderO hash dict keypairs privkey pubkey
        2dup ."Storing (" Bx. .", " Bx. .")" cr
        <b // -> builderO hash dict keypairs privkey pubkey builderI
          2 pick Blen 64 = {
            2 roll B, // previously computed signature
          } {
            5 pick 3 roll ed25519_sign_uint B, // <- hash privkey
          } cond
          // -> builderO hash dict keypairs pubkey builderI
          swap 256 B>u@
          // -> builderO hash dict keypairs builderI pubkey
          3 roll
          // -> builderO hash dict keypairs builderI pubkey
          256
        b>udict! // -> builderO hash keypairs dict ok?
        0= abort"Unable to store signature of this order" // -> dict
      } while
    drop nip 
    dict,
  b>
} : order-message()

// sign-message()
// (message_body private_key -- slice)
//
// Attaches signature to a slice representing message body.
{
  over hash swap ed25519_sign_uint
  <b 
    swap B, swap <s s,
  b>
} : sign-message()

// parse-order-message()
// (cell -- sig seqno valid_until last_pubkey hash_only? request signatures)
//
// Decodes signed request data from a message body.
{
  <s
    64 B@+
    32 u@+
    32 i@+
    256 u@+
    dup srefs {
      1 i@+
      ref@+ swap
      dict@+
    } {
      null null null
      3 roll
    } cond
  s>
} : parse-order-message()